# Universal binary on MAC

ROOT = $$PWD/../../..

macx {
	CONFIG+= i386 x86_64
	QMAKE_CXXFLAGS += -mmacosx-version-min=10.6
}

ios {
	QMAKE_CXXFLAGS += -DIOS
	CONFIG += c++11
	CONFIG+= arm64 armv7 armv7s
	
}

win32: {
	contains(QMAKE_HOST.arch, x86): {
	QMAKE_LFLAGS *= /MACHINE:X86
	!DebugBuild { GUIDO = $$ROOT/build/win32/release/GUIDOEngine.lib }
	else		{ GUIDO = $$ROOT/build/win32/debug/GUIDOEngineD.lib } 
	}
	contains(QMAKE_HOST.arch, x86_64): {
	QMAKE_LFLAGS *= /MACHINE:X64
	!DebugBuild { GUIDO = $$ROOT/build/win64/release/GUIDOEngine64.lib }
	else		{ GUIDO = $$ROOT/build/win64/debug/GUIDOEngine64D.lib }
	}
}

# GUIDOEngine library link for each platform
!DebugBuild {
	macx:LIBS += -F$$ROOT/build -framework GUIDOEngine
	ios:LIBS += -L$$ROOT/build/iOS/Release-iphoneos -lGUIDOEngine
} else {
	macx:LIBS += -F$$ROOT/build/MacOS/Debug -framework GUIDOEngine
	ios:LIBS += -L$$ROOT/build/iOS/Debug-iphoneos -lGUIDOEngine
}

unix:!macx:!ios:LIBS += -L$$ROOT/build/linux -lGUIDOEngine
win32:LIBS += $$GUIDO

INCLUDEPATH += $$ROOT/src/engine/include 
